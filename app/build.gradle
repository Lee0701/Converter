apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdk 34

    defaultConfig {
        applicationId "io.github.lee0701.converter"
        minSdk 22
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions "version"
    productFlavors {
        common {
            dimension "version"
            applicationIdSuffix ""
            versionCode 22
            versionName "0.4.4"
            buildConfigField 'boolean', 'IS_DONATION', 'false'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
        donation {
            dimension "version"
            applicationIdSuffix ".donation"
            versionCode 26
            versionName "0.4.4"
            buildConfigField 'boolean', 'IS_DONATION', 'true'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_donation",
                    appIconRound: "@mipmap/ic_launcher_donation_round"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'io.github.lee0701.converter'
    applicationVariants.configureEach { variant ->
        if (variant.productFlavors[0].name == 'common') {
            variant.mergeAssetsProvider.configure {
                doLast {
                    delete(fileTree(dir: outputDir, includes: ['**/model.tflite', '**/wordlist.txt']))
                }
            }
        }
    }
}

dependencies {
    def room_version = '2.6.0'
    def lifecycle_version = '2.6.2'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.github.skydoves:colorpickerpreference:2.0.6'
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.1"
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.5'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

}