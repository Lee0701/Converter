plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'com.google.android.gms.oss-licenses-plugin'
}

android {
    compileSdk 36

    defaultConfig {
        applicationId "io.github.lee0701.converter"
        minSdk 22
        targetSdk 36

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    flavorDimensions "version"
    productFlavors {
        common {
            dimension "version"
            applicationIdSuffix ""
            versionCode 23
            versionName "0.5.0"
            buildConfigField 'boolean', 'IS_DONATION', 'false'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
        donation {
            dimension "version"
            applicationIdSuffix ".donation"
            versionCode 27
            versionName "0.5.0"
            buildConfigField 'boolean', 'IS_DONATION', 'true'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_donation",
                    appIconRound: "@mipmap/ic_launcher_donation_round"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'io.github.lee0701.converter'
    applicationVariants.configureEach { variant ->
        if (variant.productFlavors[0].name == 'common') {
            variant.mergeAssetsProvider.configure {
                doLast {
                    delete(fileTree(dir: outputDir, includes: ['**/model.tflite', '**/wordlist.txt']))
                }
            }
        }
    }
}

dependencies {

    def room_version = '2.7.2'
    def lifecycle_version = '2.9.3'
    implementation 'org.tensorflow:tensorflow-lite-support:0.5.0'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.5.0'
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.recyclerview:recyclerview-selection:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'com.github.skydoves:colorpickerpreference:2.0.6'
    implementation 'com.google.android.gms:play-services-oss-licenses:17.2.2'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'

}