apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

def playcoreDir = file('../play-core-native-sdk')

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.github.lee0701.converter"
        minSdkVersion 21
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFile "$playcoreDir/proguard/common.pgcfg"
            proguardFile "$playcoreDir/proguard/per-feature-proguard-files"
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    buildFeatures {
        mlModelBinding true
        viewBinding true
    }

    assetPacks = [":prediction"]

    flavorDimensions "version"
    productFlavors {
        common {
            dimension "version"
            applicationIdSuffix ""
            versionCode 11
            versionName "0.1.10"
            buildConfigField 'boolean', 'IS_DONATION', 'false'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
        donation {
            dimension "version"
            applicationIdSuffix ".donation"
            versionCode 3
            versionName "0.1.2"
            buildConfigField 'boolean', 'IS_DONATION', 'true'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_donation",
                    appIconRound: "@mipmap/ic_launcher_donation_round"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    applicationVariants.all { variant ->
        if (variant.productFlavors[0].name == 'common') {
            variant.mergeAssetsProvider.configure {
                doLast {
                    delete(fileTree(dir: outputDir, includes: ['**/model.tflite', '**/wordlist.txt']))
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "com.github.skydoves:colorpickerpreference:2.0.5"
    implementation 'org.tensorflow:tensorflow-lite-support:0.2.0'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.2.1'
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'com.google.android.play:core:1.10.2'
    implementation 'com.google.android.play:core-ktx:1.8.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'


}