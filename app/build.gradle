apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.github.lee0701.converter"
        minSdkVersion 22
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    buildFeatures {
        viewBinding true
    }

    flavorDimensions "version"
    productFlavors {
        common {
            dimension "version"
            applicationIdSuffix ""
            versionCode 18
            versionName "0.4.0"
            buildConfigField 'boolean', 'IS_DONATION', 'false'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher",
                    appIconRound: "@mipmap/ic_launcher_round"
            ]
        }
        donation {
            dimension "version"
            applicationIdSuffix ".donation"
            versionCode 22
            versionName "0.4.0"
            buildConfigField 'boolean', 'IS_DONATION', 'true'
            manifestPlaceholders = [
                    appIcon: "@mipmap/ic_launcher_donation",
                    appIconRound: "@mipmap/ic_launcher_donation_round"
            ]
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'io.github.lee0701.converter'
    applicationVariants.all { variant ->
        if (variant.productFlavors[0].name == 'common') {
            variant.mergeAssetsProvider.configure {
                doLast {
                    delete(fileTree(dir: outputDir, includes: ['**/model.tflite', '**/wordlist.txt']))
                }
            }
        }
    }
}

dependencies {
    def room_version = "2.4.2"
    def lifecycle_version = "2.4.1"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0-native-mt'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.github.skydoves:colorpickerpreference:2.0.6'
    implementation 'org.tensorflow:tensorflow-lite-support:0.3.1'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.3.1'
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}